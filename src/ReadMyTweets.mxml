<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx">
	<fx:Script>
		<![CDATA[
			//each twit is stored in an array, splitted in 50 chars packets. All twits are saved in a main array.
			protected static const CHUNK_SIZE:int = 70; //max size that can be read by unofficial google translate API
			protected var twits:Array;
			protected var snd:Sound; 
			protected var sndChannel:SoundChannel;
			
			protected function readTwits(event:MouseEvent = null):void
			{
				var req:URLLoader = new URLLoader();
				req.addEventListener(Event.COMPLETE, handleTwitsLoadComplete, false, 0, true);
				req.load(new URLRequest(getFeedURL(twitterUsernameTI.text)));
				debugText.text = "loading tweets...";
			}
			
			protected function readNextTwit(event:MouseEvent):void
			{
				if(!twits) return;
				currentTwit++;
				playSound();
			}
			
			protected function getFeedURL(username:String):String
			{
				return "https://twitter.com/statuses/user_timeline/"+username+".xml?count=100";
			}
			
			protected var currentTwit:int = 0;
			protected var currentTwitChunk:int = 0;
			
			protected function getMP3URL():String
			{
				var textToRead:String = "";
				if(currentTwitChunk > twits[currentTwit].length-1)
				{
					currentTwitChunk = 0;
					currentTwit++;
				}
				if(currentTwit > twits.length-1)
				{
					currentTwit = 0;
				}
				textToRead = twits[currentTwit][currentTwitChunk];
				debugText.text = "Reading tweet "+currentTwit+" / "+(twits.length-1)
									+" chunck "+currentTwitChunk+" / "+(twits[currentTwit].length-1
									+" text: "+textToRead);
				trace("\READ: "+textToRead);
				trace("URL: http://translate.google.com/translate_tts?q="+textToRead);
				return "http://translate.google.com/translate_tts?hl="+twitterLanguageTI.text+"&q="+textToRead;
				//return "http://translate.google.com/translate_tts?q="+textToRead;
			}
			
			protected function removeLinks(text:String):String
			{
				var result:String = '';
				var pattern:RegExp = /(?<!\S)(((f|ht){1}tp[s]?:\/\/|(?<!\S)www\.)[-a-zA-Z0-9@:%_\+.~#?&\/\/=]+)/g;
				while(pattern.test(text)) result = text.replace(pattern, "");
				if(result == '') result+= text;//if there was nothing to replace
				return result;
			}
						
			protected function handleTwitsLoadComplete(event:Event):void
			{
				twits = [];
				currentTwit = 0;
				currentTwitChunk = 0;
				var tmp:XML = new XML(event.target.data);
				var tmplist:XMLList = new XMLList(tmp..text);
				trace("tmplist.length: "+tmplist.length());
				debugText.text = tmplist.length()+" tweets loaded.";
				for (var index:int = 0; index < tmplist.length(); index++) 
				{
					var tmpStr:String = String(tmplist[index]);
					trace("add twit: "+tmpStr);
					var twitChunksLength:int = Math.ceil(tmpStr.length / CHUNK_SIZE);
					var twitArray:Array = [];
					for (var chunckIndex:int = 0; chunckIndex < twitChunksLength; chunckIndex++) 
					{
						var start:int = chunckIndex * CHUNK_SIZE;
						var end:int = chunckIndex * CHUNK_SIZE + CHUNK_SIZE;
						if(chunckIndex == (twitChunksLength-1))
						{
							end = tmpStr.length;
						}
						trace("add chunck: "+removeLinks(tmpStr.slice(start, end)));
						twitArray.push(removeLinks(tmpStr.slice(start, end)));
					}
					twits.push(twitArray);
				}
				playSound();
			}
			
			public function playSound(event:Event = null):void 
			{
				if(sndChannel) stopSound();
				if(event)
				{
					currentTwitChunk++;
				}
				snd = new Sound(new URLRequest(getMP3URL()));
				sndChannel = snd.play();
				if(!sndChannel.hasEventListener(Event.SOUND_COMPLETE))
				{
					sndChannel.addEventListener(Event.SOUND_COMPLETE, playSound, false, 0, true);
				}
			}   
			
			public function stopSound():void 
			{
				sndChannel.stop();
			} 
		]]>
	</fx:Script>
	<s:VGroup verticalCenter="0" horizontalCenter="0">
		<s:Label id="debugText" />
		<s:Form verticalCenter="0" horizontalCenter="0">
			<s:FormItem label="twitter username">
				<s:TextInput id="twitterUsernameTI" text="html5douche" />
			</s:FormItem>
			<s:FormItem label="language (en for english etc.)">
				<s:TextInput id="twitterLanguageTI" text="en" />
			</s:FormItem>
			<s:Button label="READ" click="readTwits(event)" />
			<s:Button label="READ NEXT" click="readNextTwit(event)" />
		</s:Form>
	</s:VGroup>
</s:Application>
